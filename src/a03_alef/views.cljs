(ns a03-alef.views
  (:require
   [re-frame.core :as re-frame]
   [a03-alef.subs :as subs :refer [<sub]]
   [a03-alef.events :as events :refer [>evt]]))

;; ========== INDIVIDUAL COMPONENTS ============================================
(defn hr
  []
  [:hr {:style {:border "none"
                :height ".1px"
                :margin "10px 15px"
                :background-color "silver"}}])

(defn input-panel
  []
  (let [value (<sub [::subs/hash-entry])]
    [:div.panel
     [:input {:type :button
              :value "Back"
              :on-click #(>evt [::events/go-back])}]
     [:input {:type :text
              :value value
              :on-change #(>evt [::events/hash-entry (.. % -target -value)])}]
     [:input {:type :button
              :value "Submit path"
              :on-click #(>evt [::events/refocus-hash value])}]]))

(defn info-panel
  []
  [:div.b
   [:h4.sh "The focus tree"]
   [:code.ip (<sub [::subs/node-str])]
   [hr]
   [:h4.sh "The base tree, generated by request"]
   [:code.ip (<sub [::subs/tree-str])]])

(defn cap
  []
  (let [display (<sub [::subs/content-focus])]
    [:div.b
     (if (or (nil? display) (empty? display))
       [:span.t "There is no content at this node."]
       display)]))

(defn brick
  [condensed]
  (let [address (-> condensed last .toUpperCase)]
    [:a.a
     {:on-click #(>evt [::events/refocus-hash condensed])}
     [:li.brick address]])) ;HERE!

(defn wythe
  []
  (let [signatures (<sub [::subs/children-signatures])]
    (if (nil? signatures)
      [:span.b.t "There are no children to populate this wythe."]
      [:ul.wythe
       (for [child signatures]
         ^{:key child} [brick child])])))

;; ========== COMPONENT ASSEMBLY ===============================================
(defn main-panel []
  [:div#wrapper.i
   [:h1 (<sub [::subs/name])]
   [hr]
   [:div
    {:style {:display "flex"
             :flex-flow "row wrap"}}
    [:div
     {:style {:min-width "450px"
              :min-height "450px"}}
     [input-panel]
     [:div.b
      [cap]
      [wythe]]]
    [:div
     [info-panel]]]])
